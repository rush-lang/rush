import std.collections

export func each<T>(src: extends iterable<T>, fn: T -> void)
	for item in src
		fn(item)

export func any<T>(src: extends iterable<T>)
	return src.iterator().next()

export func any<T>(src: extends iterable<T>, pred: T -> bool)
	for item in src
		if pred(item) return true
	return false

export func all<T>(src: extends iterable<T>, pred: T -> bool)
	for item in src
		if !pred(item) return false
	return true

export func take<T>(var count: int, src: extends iterable<T>)
	let it = src.iterator()
	while count-- > 0 && it.next()
		yield it.value

export func take<T>(src: extends iterable<T>, pred: T -> bool)
	let it = src.iterator()
	while it.next() && pred(it.value)
		yield it.value

export func skip<T>(var count: int, src: extends iterable<T>)
	let it = src.iterator()
	while count-- > 0 && it.next()
	while it.next() yield it.value

export func skip<T>(src: extends iterable<T>, pred: T -> bool)
	let it = src.iterator()
	while it.next() && pred(it.value)
	while it.next() yield it.value

export func map<T, R>(src: extends iterable<T>, fn: T -> R)
	for item in src
		yield fn(item)

export func flatmap<T, R>(src: extends iterable<T>, fn: T -> iterable<R>)
	for item in src
		for inner in fn(item)
			yield inner

export func filter<T>(src: extends iterable<T>, fn: T -> bool)
	for item in src
		if fn(item) yield item

export func reduce<T>(src: extends iterable<T>, fn: (T, T) -> T)
	return reduce(
		src.skip(1),
		src.first(), fn)

export func reduce<T, R>(src: extends iterable<T>, var init: R, fn: (R, T) -> R)
	for item in src
		init = fn(init, item)
	return init

func chunk_part<T>(iter: iterator<T>, var size: int)
	while iter.next() && size-- > 0
		yield iter.value

export func chunk<T>(src: extends iterable<T>, size: int)
	let it = src.iterator()
	while it.next()
		yield chunk_part(it, size)

export func partition<T>(src: extends iterable<T>, parts: int)
	=> chunk(src, src.count() / parts + 1)

## interweaves the delim value between the collection of items.
export func lace<T>(src: extends iterable<T>, delim: T)
	let it = src.iterator()
	if it.next()
		yield it.value
		while it.next()
			yield delim
			yield it.value


export func first<T>(src: extends iterable<T>)
   let it = src.iterator()
   if !it.next()
		throw new sequence_error("The collection is empty.")
   return it.value


export func first<T>(src: extends iterable<T>, pred: T -> bool)
	let it = src.iterator()
	if !it.next()
		throw new sequence_error("The collection is empty.")

	if pred(it.value)
		return it.value

	while it.next()
		if pred(it.value)
			return it.value

	throw new sequence_error("Item not found.")


export func last<T>(src: extends indexable<T>)
   if src.length == 0
      throw new sequence_error("The collection is empty.")

   return src[src.length - 1]


export func last<T>(src: extends iterable<T>)
   let it = src.iterator()
   if it.next() == false
      throw new sequence_error("The collection is empty.")

   while it.next()
   return it.value